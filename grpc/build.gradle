plugins {
    id 'java-library'
    id 'com.google.protobuf'
}

//用 Kotlin DSL 无法生成代码

dependencies {
    //grpc
    def grpcVersion = '1.37.1'
    api "io.grpc:grpc-okhttp:$grpcVersion"
    api "io.grpc:grpc-android:$grpcVersion"
    api "io.grpc:grpc-protobuf:$grpcVersion"
    api "io.grpc:grpc-stub:$grpcVersion"
    implementation 'org.apache.tomcat:annotations-api:6.0.53'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.16.0-rc-1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.37.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir "java"
                }
            }
            task.plugins {
                grpc {
                    outputSubDir "java"
                }
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task getBiliProto(type: Copy) {
    def biliApk6220 = new File(buildDir, "download/biliApk_6.22.0.apk")
    if (!biliApk6220.exists()) {
        biliApk6220.parentFile.mkdirs()
        new URL("https://dl.love4taylor.com/bilibili-apk/arm64-v8a/iBiliPlayer-apinkRelease-6.22.0-b5189578.apk")
                .withInputStream { i -> biliApk6220.withOutputStream { it << i } }
    }

    from(zipTree(biliApk6220)) {
        include "bilibili/**"
        include "github.com/**"
    }
    into new File(projectDir, "src/main/proto")
}

afterEvaluate {
    extractIncludeProto.dependsOn getBiliProto
}